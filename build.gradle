allprojects {
  apply plugin: 'java'
  apply plugin: 'eclipse'
  apply plugin: 'maven'
  apply plugin: 'idea'

  version = "0.0.1"

  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11

  group = "kz.greetgo.nf36"

  buildscript {
    repositories {
      mavenLocal()
      mavenCentral()
    }
  }

  repositories {
    mavenLocal()
    if (System.getenv('GG_REPO')?.trim()) maven { url System.getenv('GG_REPO').trim() }
    mavenCentral()
  }
}

@SuppressWarnings("GroovyAssignabilityCheck")
def extractLastGitCommitId() {
  def gitFolder = "${rootProject.projectDir}/.git/"

  def takeFromHash = 17

  if (!new File(gitFolder + "HEAD").exists()) {
    return "UNKNOWN"
  }

  /*
   * '.git/HEAD' contains either
   *      in case of detached head: the currently checked out commit hash
   *      otherwise: a reference to a file containing the current commit hash
   */
  def head = new File(gitFolder + "HEAD").text.split(":") // .git/HEAD
  def isCommit = head.length == 1 // e5a7c79edabbf7dd39888442df081b1c9d8e88fd
  // def isRef = head.length > 1     // ref: refs/heads/master

  if (isCommit) {
    return head[0].trim().take(takeFromHash) // e5a7c79edabb
  }

  def reference = head[1].trim()

  def refHead = new File(gitFolder + reference) // .git/refs/heads/master
  if (refHead.exists()) {
    return refHead.text.trim().take(takeFromHash)
  }

  def packedRefsFile = new File(gitFolder + "packed-refs")

  if (packedRefsFile.exists()) {

    def lines = packedRefsFile.text.split("\n")

    for (String line : lines) {

      if (line.trim().startsWith("#")) {
        continue
      }

      def split = line.split("\\s+")
      if (split.length == 2) {

        if (split[1] == reference) {
          return split[0]
        }

      }
    }

  }

  throw new Exception("Cannot find commit id")
}

subprojects {

  dependencies {
    ext.greetgoDbVersion = '2.3.5'
    ext.configVersion = "1.0.2"
    ext.greetgoUtilVersion = "0.0.27"
    ext.depinjectVersion = "2.2.0"
    ext.greetgoJavaCompilerVersion = "2.0.1"
    ext.pgDriverVersion = "9.4-1202-jdbc42"
    ext.greetgoClassScannerVersion = "2.0.3"
    ext.testNgVersion = "6.14.3"

    testCompile "org.testng:testng:$testNgVersion"
    testCompile 'org.easytesting:fest-assert-core:2.0M10'
  }

  tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
  }

  test.useTestNG()

  idea.module.downloadSources = true
  idea.module.downloadJavadoc = true
  idea.module.inheritOutputDirs = false
  idea.module.outputDir = file("$buildDir/classes/main/")
  idea.module.testOutputDir = file("$buildDir/classes/test/")

  [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
  [compileJava, compileTestJava]*.options*.debugOptions*.debugLevel = "source,lines,vars"

  sourceSets.main.java.srcDirs = ["src"]
  sourceSets.test.java.srcDirs = ["test_src"]
  sourceSets.main.resources.srcDirs = ["src_resources"]
  sourceSets.test.resources.srcDirs = ["test_resources"]

  sourceSets.main.resources {
    srcDirs += sourceSets.main.java.srcDirs
    exclude '**/*.java'
  }
  sourceSets.test.resources {
    srcDirs += sourceSets.test.java.srcDirs
    exclude '**/*.java'
  }

  jar {
    manifest {
      attributes 'Specification-Title': project.name
      attributes 'Specification-Version': version
      attributes 'Specification-Vendor': "greetgo!"
      attributes 'Created-By': System.getProperty("user.name")
      attributes 'Created-At': new Date().format("yyyy-MM-dd/HH:mm:ss")
      attributes 'Git-Last-Commit-Id': extractLastGitCommitId()
    }
  }
}
